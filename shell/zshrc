source ~/dotfiles/shell/shell_common.sh

bindkey -e

HISTFILE=~/.history
HISTSIZE=1000
SAVEHIST=1000
# not to save duplicated histry
setopt hist_ignore_dups
setopt share_history

# The following lines were added by compinstall
zstyle :compinstall filename '~/.zshrc'
autoload -Uz compinit
compinit
# End of lines added by compinstall

DIRSTACKSIZE=100
disable r

# Enable cdr, add-zsh-hook
autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs

# cdr
zstyle ':completion:*' recent-dirs-insert both
zstyle ':chpwd:*' recent-dirs-max 500
zstyle ':chpwd:*' recent-dirs-default true

bindkey "^r" history-incremental-pattern-search-backward
bindkey "^s" history-incremental-pattern-search-forward
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^n" history-beginning-search-forward-end
bindkey "^p" history-beginning-search-backward-end

# execution without the command
alias -s rb='ruby'
alias -s py='python'
alias -s php='php -f'

setopt print_eight_bit
setopt globdots
# correct spell
setopt correct
# evaluate prompt every time
setopt prompt_subst
setopt AUTO_PUSHD
# enable comments in interactive terminal
setopt interactivecomments

autoload -U colors; colors

# completions
zstyle ':completion:*' menu select=2
zstyle ':completion:*' verbose yes
zstyle ':completion:*' completer _ssh _complete _match _approximate _prefix _list _expand _history
zstyle ':completion:*:messages' format ${fg_bold[yellow]}'%d'${reset_color}
zstyle ':completion:*:warnings' format ${fg[red]}'No matches for:'${fg[yellow]}' %d'${reset_color}
zstyle ':completion:*:descriptions' format ${fg[yellow]}'%B%d%b'${reset_color}
zstyle ':completion:*:corrections' format ${fg_bold[yellow]}'%B%d '${fg[red]}'(errors: %e)%b'${reset_color}
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' use-cache true
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:cd:*' ignore-parents parent pwd
zstyle ':completion:*:*files' ignored-patterns '*?.o' '*?~' '*\#'
export LS_COLORS='di=34:ln=35:so=32:pi=33:ex=31:bd=46;34:cd=43;34:su=41;30:sg=46;30:tw=42;30:ow=43;30'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}


# ----- PROMPT -----
## PROMPT
PROMPT=$'[%~] %# ' # %~: pwd
## RPROMPT
RPROMPT=$'`branch-status-check`'

# github user contents
# https://gist.github.com/otiai10/8034038
# {{{ methods for RPROMPT
function branch-status-check {
    local prefix branchname suffix
        # Skip in the .git firectory
        if [[ "$PWD" =~ '/\.git(/.*)?$' ]]; then
            return
        fi
        branchname=`get-branch-name`
        # No branch name
        if [[ -z $branchname ]]; then
            return
        fi
        prefix=`get-branch-status` # Returns color
        suffix='%{'${reset_color}'%}'
        echo ${prefix}${branchname}${suffix}
}
function get-branch-name {
    echo `git rev-parse --abbrev-ref HEAD 2> /dev/null`
}
function get-branch-status {
    local res color
        output=`git status --short 2> /dev/null`
        if [ -z "$output" ]; then
            res=':' # status Clean
            color='%{'${fg[green]}'%}'
        elif [[ $output =~ "[\n]?\?\? " ]]; then
            res='?:' # Untracked
            color='%{'${fg[yellow]}'%}'
        elif [[ $output =~ "[\n]? M " ]]; then
            res='M:' # Modified
            color='%{'${fg[red]}'%}'
        else
            res='A:' # Added to commit
            color='%{'${fg[cyan]}'%}'
        fi
        # echo ${color}${res}'%{'${reset_color}'%}'
        echo ${color}
}
# }}}

# added by travis gem
[ -f ~/.travis/travis.sh ] && source ~/.travis/travis.sh

# Functions
function cd_using_peco {
    eval local selected_dir=$(eval "$1 | peco")
    if [ -n "$selected_dir" ]; then
        cd "${selected_dir}"
    fi
}
function pr {
    cd_using_peco "find . -type d 2>/dev/null | sort"
}
function ph {
    cd_using_peco "cdr -l | awk '{ print \$2 }'"
}
